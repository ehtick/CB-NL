PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX cbnl-term: <https://bimloket.nl/data/kernmodel/thesaurus#>


SELECT 
	(STR(?ConceptURI) as ?Concept)
	(STR(?BroaderURI) as ?Broader)
	(STR(?labelNLlit) as ?labelNL)
	(STR(?labelENlit) as ?labelEN)
	(GROUP_CONCAT(?definition ; separator="|") AS ?definitions)  
	(GROUP_CONCAT(?altLabelNL ; separator="|") AS ?altLabelsNL)  
	(GROUP_CONCAT(?altLabelEN ; separator="|") AS ?altLabelsEN) 
	(STR(?referencelit) as ?reference)
	(GROUP_CONCAT(?comment ; separator="|") AS ?comments) 

WHERE { 
    ?ConceptURI a skos:Concept .
    OPTIONAL {
        ?ConceptURI skos:prefLabel ?labelNLlit .
        FILTER (LANG(?labelNLlit) = "nl")
    } .
    OPTIONAL {
        ?ConceptURI skos:prefLabel ?labelENlit .
        FILTER (LANG(?labelENlit) = "en")
    } .
    OPTIONAL {
        ?ConceptURI skos:altLabel ?altLabelNL .
    	FILTER (LANG(?altLabelNL) = "nl")
    } .
    OPTIONAL {
        ?ConceptURI skos:altLabel ?altLabelEN .
    	FILTER (LANG(?altLabelEN) = "en")
    } .
   	OPTIONAL {?ConceptURI skos:definition ?definition } .
    OPTIONAL {?ConceptURI dct:references ?referencelit } .
    OPTIONAL {?ConceptURI rdfs:comment ?comment } .
   	OPTIONAL {?ConceptURI skos:broader ?BroaderURI } .    
}  
GROUP BY 
	?ConceptURI
	?BroaderURI
	?labelNLlit
	?labelENlit
	?referencelit